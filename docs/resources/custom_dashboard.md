---
# generated by https://github.com/hashicorp/terraform-plugin-docs
page_title: "orcasecurity_custom_dashboard Resource - orcasecurity"
subcategory: ""
description: |-
  Provides a custom dashboard resource.
---

# orcasecurity_custom_dashboard (Resource)

Provides a custom dashboard resource.

## Example Usage

```terraform
//2-widget custom dashboard
resource "orcasecurity_custom_dashboard" "tf-custom-dash-1" {
  name               = "Orca Custom Dashboard 1"
  filter_data        = {}
  organization_level = true
  view_type          = "dashboard"
  extra_params = {
    description = "my 1st simple dashboard"
    widgets_config = [
      {
        id   = "cloud-accounts-inventory"
        size = "sm"
      },
      {
        id   = "security-score-benchmark"
        size = "md"
      }
    ]
  }
}
```

## Built-in Widget IDs

Use these IDs in `widgets_config` when adding Orca built-in widgets to a dashboard. Widget IDs are slugified versions of the names from [Orca Dashboard Widgets](https://docs.orcasecurity.io/v1/docs/available-dashboard-widgets). For custom widgets, use the `id` attribute of an `orcasecurity_custom_widget` resource.

| Widget Name (Orca docs)          | Widget ID                               |
|----------------------------------|-----------------------------------------|
| Accounts (Inventory)             | `cloud-accounts-inventory`              |
| Security Score Benchmark         | `security-score-benchmark`              |
| Security Score Breakdown         | `security-score-breakdown`              |
| Alerts on Exposed Assets         | `alerts-on-exposed-assets`              |
| Top Attack Path Entry Points     | `top-attack-path-entry-points`          |
| Top Attack Path Target Assets    | `top-attack-path-target-assets`         |
| Alerts Pending Action            | `alerts-pending-action`                 |
| Alerts by Risk Level             | `alerts-by-severity`                    |
| Risk Categories Alerts Over Time | `risk-categories-alerts-over-time`      |
| Created & Resolved Alerts        | `created-and-resolved-alerts-over-time` |
| Fixable Vulnerabilities          | `fixable-vulnerabilities`               |
| Vulnerable Assets (by max CVSS)  | `assets-max-cvss`                       |
| Compliance Overview              | `compliance-overview`                   |
| Top Alerts                       | `top-alerts`                            |
| Major Insights                   | `major-insights`                        |
| Alert Inventory                  | `alert-inventory`                       |
| Asset Inventory                  | `asset-inventory`                       |

For other widgets, derive the ID by converting the name to lowercase and replacing spaces with hyphens (e.g., "Top Sensitive Data Findings" â†’ `top-sensitive-data-findings`).

## Using Custom Widgets

Custom widgets are created with the `orcasecurity_custom_widget` resource. The widget receives an `id` from Orca after creation (computed attribute). Reference that `id` in `widgets_config` to add the custom widget to a dashboard. You can mix built-in and custom widgets in the same dashboard.

**1. Create the custom widget:**

```terraform
resource "orcasecurity_custom_widget" "my_widget" {
  name               = "My Custom Widget"
  organization_level = true
  extra_params = {
    type                = "donut"
    empty_state_message = "No data found"
    default_size        = "sm"
    is_new              = true
    subtitle            = ""
    description         = ""
    settings = {
      columns = ["alert", "asset"]
      request_params = {
        query = jsonencode({
          keys   = ["Alert"]
          type   = "object_set"
          models = ["Alert"]
        })
        group_by      = ["RiskLevel"]
        group_by_list = ["RiskLevel"]
      }
      field = { name = "RiskLevel", type = "str" }
    }
  }
}
```

**2. Add it to a dashboard using its `id`:**

```terraform
resource "orcasecurity_custom_dashboard" "my_dashboard" {
  name               = "Dashboard with Custom Widget"
  organization_level = true
  filter_data        = {}
  view_type          = "dashboard"
  extra_params = {
    description = ""
    widgets_config = [
      { id = orcasecurity_custom_widget.my_widget.id, size = "sm" },
      { id = "cloud-accounts-inventory", size = "sm" }
    ]
  }
}
```

See the [orcasecurity_custom_widget](custom_widget.md) resource for full schema and more examples (table-type, alert widgets, etc.).

## Importing

Existing dashboards created in the Orca UI can be imported into Terraform.

**Get the dashboard ID:** Open the dashboard in the Orca UI and check the URL. The dashboard ID (preference ID) appears in the URL path.

**Import command:**

```bash
terraform import orcasecurity_custom_dashboard.<resource_name> <dashboard_id>
```

Example:

```bash
terraform import orcasecurity_custom_dashboard.my_dashboard abc123-def456-ghi789
```

Add a placeholder resource block to your configuration first, then run the import. After import, run `terraform plan` to align your configuration with the imported state.

<!-- schema generated by tfplugindocs -->

## Schema

### Required

- `extra_params` (Attributes) (see [below for nested schema](#nestedatt--extra_params))
- `filter_data` (Map of String) Should be left empty for custom dashboards.
- `name` (String) Custom dashboard title.
- `organization_level` (Boolean) If set to true, it is a shared dashboard (can be viewed by any member of your Orca org). If set to false, it is a personal dashboard (can be viewed only by you, not other members of your Orca org).
- `view_type` (String) Should be set to 'dashboard' for custom dashboards.

### Read-Only

- `id` (String) Custom dashboard ID.

<a id="nestedatt--extra_params"></a>

### Nested Schema for `extra_params`

Required:

- `description` (String)
- `widgets_config` (Attributes List) (see [below for nested schema](#nestedatt--extra_params--widgets_config))

<a id="nestedatt--extra_params--widgets_config"></a>

### Nested Schema for `extra_params.widgets_config`

Required:

- `id` (String) ID of the identified widget.
- `size` (String) Size of the identified widget. Possible values are sm (small), md (medium), or lg (large).
